{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "0d5b3ec7-abf1-4276-9a1f-8da091bf41c5",
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "[gfort kernel] module objects created successfully: mod_arrays.o"
     ]
    }
   ],
   "source": [
    "%fcflags: -w\n",
    "%module: mod_arrays\n",
    "module mod_arrays\n",
    "\n",
    "  ! Utility functions that operate on arrays.\n",
    "\n",
    "  implicit none\n",
    "\n",
    "  private\n",
    "  public :: argsort, average, crossneg, crosspos, intdate, moving_average,&\n",
    "            moving_std, reverse, std\n",
    "\n",
    "contains\n",
    "\n",
    "  pure function argsort(x) result(a)\n",
    "    ! Returns indices that sort x from low to high.\n",
    "    real, intent(in):: x(:)\n",
    "    integer :: a(size(x))\n",
    "    integer :: i, i0, tmp1\n",
    "    real :: tmp2\n",
    "    real :: xwork(size(x))\n",
    "    a = [(real(i), i = 1, size(x))]\n",
    "    xwork = x\n",
    "    do i = 1, size(x) - 1\n",
    "      i0 = minloc(xwork(i:), 1) + i - 1\n",
    "      if (i0 /= i) then\n",
    "        tmp2 = xwork(i)\n",
    "        xwork(i) = xwork(i0)\n",
    "        xwork(i0) = tmp2\n",
    "        tmp1 = a(i)\n",
    "        a(i) = a(i0)\n",
    "        a(i0) = tmp1\n",
    "      end if\n",
    "    end do\n",
    "  end function argsort\n",
    "\n",
    "  pure real function average(x)\n",
    "    ! Returns a average of x.\n",
    "    real, intent(in) :: x(:)\n",
    "    average = sum(x) / size(x)\n",
    "  end function average\n",
    "\n",
    "  pure function crossneg(x, w) result(res)\n",
    "    ! Returns indices where input array x crosses its\n",
    "    ! moving average with window w from positive to negative.\n",
    "    real, intent(in) :: x(:)\n",
    "    integer, intent(in) :: w\n",
    "    integer, allocatable :: res(:)\n",
    "    real, allocatable :: xavg(:)\n",
    "    logical, allocatable :: greater(:), smaller(:)\n",
    "    integer :: i\n",
    "    res = [(i, i = 2, size(x))]\n",
    "    xavg = moving_average(x, w)\n",
    "    greater = x > xavg\n",
    "    smaller = x < xavg\n",
    "    res = pack(res, smaller(2:) .and. greater(:size(x)-1))\n",
    "  end function crossneg\n",
    "\n",
    "  pure function crosspos(x, w) result(res)\n",
    "    ! Returns indices where input array x crosses its\n",
    "    ! moving average with window w from negative to positive.\n",
    "    real, intent(in) :: x(:)\n",
    "    integer, intent(in) :: w\n",
    "    integer, allocatable :: res(:)\n",
    "    real, allocatable :: xavg(:)\n",
    "    logical, allocatable :: greater(:), smaller(:)\n",
    "    integer :: i\n",
    "    res = [(i, i = 2, size(x))]\n",
    "    xavg = moving_average(x, w)\n",
    "    greater = x > xavg\n",
    "    smaller = x < xavg\n",
    "    res = pack(res, greater(2:) .and. smaller(:size(x)-1))\n",
    "  end function crosspos\n",
    "\n",
    "  pure elemental integer function intdate(t)\n",
    "    ! Converts a time stamp in format YYYY-mm-dd to integer.\n",
    "    character(len=10), intent(in) :: t\n",
    "    character(len=8) :: str\n",
    "    str = t(1:4) // t(6:7) // t(9:10)\n",
    "    read(str, *) intdate\n",
    "  end function intdate\n",
    "\n",
    "  pure function moving_average(x, w) result(res)\n",
    "    ! Returns the moving average of x with one-sided window w.\n",
    "    real, intent(in) :: x(:)\n",
    "    integer, intent(in) :: w\n",
    "    real :: res(size(x))\n",
    "    integer :: i, i1\n",
    "    do i = 1, size(x)\n",
    "      i1 = max(i-w, 1)\n",
    "      res(i) = average(x(i1:i))\n",
    "    end do \n",
    "  end function moving_average\n",
    "\n",
    "  pure function moving_std(x, w) result(res)\n",
    "    ! Returns the moving standard deviation of x with one-sided window w.\n",
    "    real, intent(in) :: x(:)\n",
    "    integer, intent(in) :: w\n",
    "    real :: res(size(x))\n",
    "    integer :: i, i1\n",
    "    do i = 1, size(x)\n",
    "      i1 = max(i-w, 1)\n",
    "      res(i) = std(x(i1:i))\n",
    "    end do \n",
    "  end function moving_std\n",
    "\n",
    "  pure function reverse(x)\n",
    "    ! Reverses the order of elements of x.\n",
    "    real, intent(in) :: x(:)\n",
    "    real :: reverse(size(x))\n",
    "    reverse = x(size(x):1:-1)\n",
    "  end function reverse\n",
    "\n",
    "  pure real function std(x)\n",
    "    ! Returns the standard deviation of x.\n",
    "    real, intent(in) :: x(:)\n",
    "    std = sqrt(average((x - average(x))**2))\n",
    "  end function std\n",
    "\n",
    "end module mod_arrays"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "a508ad7b-aad8-418b-8178-fe88127a4efe",
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "[gfort kernel] module objects created successfully: mod_alloc.o"
     ]
    }
   ],
   "source": [
    "%fcflags: -w\n",
    "%module: mod_alloc\n",
    "module mod_alloc\n",
    "\n",
    "  implicit none\n",
    "\n",
    "  private\n",
    "  public :: alloc, free\n",
    "\n",
    "contains\n",
    "\n",
    "  subroutine alloc(a, n)\n",
    "    real, allocatable, intent(in out) :: a(:)\n",
    "    integer, intent(in) :: n\n",
    "    integer :: stat\n",
    "    character(len=100) :: errmsg\n",
    "    if (allocated(a)) call free(a)\n",
    "    allocate(a(n), stat=stat, errmsg=errmsg)\n",
    "    if (stat > 0) error stop errmsg\n",
    "  end subroutine alloc\n",
    "\n",
    "  subroutine free(a)\n",
    "    real, allocatable, intent(in out) :: a(:)\n",
    "    integer :: stat\n",
    "    character(len=100) :: errmsg\n",
    "    if (.not. allocated(a)) return\n",
    "    deallocate(a, stat=stat, errmsg=errmsg)\n",
    "    if (stat > 0) error stop errmsg\n",
    "  end subroutine free\n",
    "\n",
    "end module mod_alloc"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "bbc258a6-db2b-4250-afb2-25816bb5bdf1",
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "[gfort kernel] module objects created successfully: mod_io.o"
     ]
    }
   ],
   "source": [
    "%module: mod_io\n",
    "module mod_io\n",
    "\n",
    "  ! A helper module for parsing stock price data in csv format.\n",
    "\n",
    "  use mod_alloc, only: alloc\n",
    "\n",
    "  implicit none\n",
    "\n",
    "  private\n",
    "  public :: read_stock, write_stock\n",
    "\n",
    "contains\n",
    "\n",
    "  integer function num_records(filename)\n",
    "    ! Return the number of records (lines) of a text file.\n",
    "    character(len=*), intent(in) :: filename\n",
    "    integer :: fileunit\n",
    "    open(newunit=fileunit, file=filename)\n",
    "    num_records = 0\n",
    "    do\n",
    "      read(unit=fileunit, fmt=*, end=1)\n",
    "      num_records = num_records + 1\n",
    "    end do\n",
    "    1 continue\n",
    "    close(unit=fileunit)\n",
    "  end function num_records\n",
    "\n",
    "  subroutine read_stock(filename, time, open, high, low, close, adjclose, volume)\n",
    "    ! Read daily stock prices from a csv file.\n",
    "    character(len=*), intent(in) :: filename\n",
    "    character(len=:), allocatable, intent(in out) :: time(:)\n",
    "    real, allocatable, intent(in out) :: open(:), high(:), low(:),&\n",
    "                                         close(:), adjclose(:), volume(:)\n",
    "    integer :: fileunit, n, nm\n",
    "    nm = num_records(filename) - 1\n",
    "    if (allocated(time)) deallocate(time)\n",
    "    allocate(character(len=10) :: time(nm))\n",
    "    call alloc(open, nm)\n",
    "    call alloc(high, nm)\n",
    "    call alloc(low, nm)\n",
    "    call alloc(close, nm)\n",
    "    call alloc(adjclose, nm)\n",
    "    call alloc(volume, nm)\n",
    "    open(newunit=fileunit, file=filename)\n",
    "    read(fileunit, fmt=*, end=1)\n",
    "    do n = 1, nm\n",
    "      read(fileunit, fmt=*, end=1) time(n), open(n),&\n",
    "        high(n), low(n), close(n), adjclose(n), volume(n)\n",
    "    end do\n",
    "    1 close(fileunit)\n",
    "  end subroutine read_stock\n",
    "\n",
    "  subroutine write_stock(filename, time, price, mvavg, mvstd)\n",
    "    ! Write derived stock data to file.\n",
    "    character(len=*), intent(in) :: filename\n",
    "    character(len=:), allocatable, intent(in) :: time(:)\n",
    "    real, intent(in) :: price(:), mvavg(:), mvstd(:)\n",
    "    integer :: fileunit, n\n",
    "    open(newunit=fileunit, file=filename)\n",
    "    do n = 1, size(time)\n",
    "      write(fileunit, fmt=*) time(n), price(n), mvavg(n), mvstd(n)\n",
    "    end do\n",
    "    close(fileunit)\n",
    "  end subroutine write_stock \n",
    "\n",
    "end module mod_io"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "a5335d29-f739-4155-bed9-466e5193903a",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      " 2000-01-03 through 2018-05-14\n",
      " Symbol, Gain (USD), Relative gain (%)\n",
      " *************************************\n",
      " AAPL   184.594589            5192\n",
      " AMZN   1512.16003            1692\n",
      " CRAY   9.60000038              56\n",
      " CSCO   1.71649933               4\n",
      " HPQ    1.55270004               7\n",
      " IBM    60.9193039              73\n",
      " INTC   25.8368015              89\n",
      " MSFT   59.4120979             154\n",
      " NVDA   251.745300            6964\n",
      " ORCL   20.3501987              77\n"
     ]
    }
   ],
   "source": [
    "%fcflags: mod_arrays.o mod_io.o mod_alloc.o\n",
    "program stock_gain\n",
    "\n",
    "    use mod_arrays, only : reverse\n",
    "    use mod_io,     only : read_stock\n",
    "    \n",
    "    implicit none\n",
    "    \n",
    "    character(len=4), allocatable :: symbols(:)\n",
    "    character(len=:), allocatable :: time   (:)\n",
    "    \n",
    "    real, allocatable :: open(:), high(:), low(:), &\n",
    "    close(:), adjclose(:), volume(:)\n",
    "    \n",
    "    integer :: n\n",
    "    real    :: gain\n",
    "    \n",
    "    symbols = ['AAPL', 'AMZN', 'CRAY', 'CSCO', 'HPQ ', 'IBM ', & \n",
    "               'INTC', 'MSFT', 'NVDA', 'ORCL']\n",
    "               \n",
    "    do n = 1, size(symbols)\n",
    "    \n",
    "        call read_stock('data/' // trim(symbols(n)) // '.csv', time, open, high, low, close, adjclose, volume)\n",
    "        \n",
    "        adjclose = reverse(adjclose)\n",
    "        gain = (adjclose(size(adjclose)) - adjclose(1))\n",
    "        \n",
    "        if (n == 1) then\n",
    "            print *, &\n",
    "              time(size(time)) // ' through ' // time(1)\n",
    "            print *, 'Symbol, Gain (USD), Relative gain (%)'\n",
    "            print *, '*************************************'\n",
    "        end if\n",
    "        \n",
    "        print *, symbols(n), gain, &\n",
    "          nint(gain / adjclose(1) * 100)\n",
    "          \n",
    "  end do\n",
    "  \n",
    "end program stock_gain"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "gfortran",
   "language": "Fortran",
   "name": "gfort_spec"
  },
  "language_info": {
   "file_extension": "f90",
   "mimetype": "text/plain",
   "name": "fortran"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
